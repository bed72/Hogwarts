plugins {
    id 'kotlin-kapt'
    id 'kotlinx-serialization'
    id 'com.android.application'
    id 'dagger.hilt.android.plugin'
    id 'org.jetbrains.kotlin.android'
    id 'androidx.navigation.safeargs'
}

apply plugin: 'kotlin-parcelize'

apply from: "$rootDir/config/detekt/detekt.gradle"

def properties = new Properties()
def key = rootProject.file("secret.properties")
properties.load(new FileInputStream(key))

android {
    compileSdk 33
    namespace "com.bed.seller"

    defaultConfig {
        minSdk 28
        targetSdk 33
        versionCode 1
        versionName '1.0'
        applicationId "com.bed.seller"

        testInstrumentationRunnerArguments clearPackageData: 'true'
        testInstrumentationRunner 'androidx.test.runner.AndroidJUnitRunner'

        buildConfigField 'String', 'API_KEY', properties['API_KEY']
        buildConfigField 'String', 'BASE_URL', properties['BASE_URL']
        buildConfigField 'String', 'DATA_STORE', properties['DATA_STORE']

    }

    buildFeatures {
        viewBinding true
    }

    testOptions {
        animationsDisabled = true
    }

    buildTypes {
        debug {
            debuggable true
            applicationIdSuffix '.debug'
        }

        staging {
            initWith debug
            minifyEnabled true
            shrinkResources true
            applicationIdSuffix '.staging'
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro',
                    'proguard-rules-staging.pro'
        }

        release {
            minifyEnabled true
            shrinkResources true
            signingConfig signingConfigs.debug
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }

    kotlinOptions {
        jvmTarget = '17'
        freeCompilerArgs += ['-opt-in=kotlin.RequiresOptIn']
    }

}

dependencies {

    implementation project(':core')

    // AndroidX
    implementation 'androidx.core:core-ktx:1.10.1'
    implementation 'androidx.appcompat:appcompat:1.6.1'
    implementation 'androidx.core:core-splashscreen:1.0.1'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'androidx.datastore:datastore-preferences:1.0.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'

    implementation "androidx.navigation:navigation-ui-ktx:$nav_version"
    implementation "androidx.navigation:navigation-fragment-ktx:$nav_version"

    // ViewModel and LiveData
    def lifecycle_version = '2.6.1'
    implementation "androidx.lifecycle:lifecycle-runtime-ktx:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-livedata-ktx:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:$lifecycle_version"

    // Hilt
    kapt "com.google.dagger:hilt-android-compiler:$hilt_version"
    implementation "com.google.dagger:hilt-android:$hilt_version"

    // Ktor
    implementation "io.ktor:ktor-client-core:$ktor_version"
    implementation "io.ktor:ktor-client-okhttp:$ktor_version"
    implementation "io.ktor:ktor-client-logging:$ktor_version"
    implementation "io.ktor:ktor-client-content-negotiation:$ktor_version"
    implementation "io.ktor:ktor-serialization-kotlinx-json:$ktor_version"

    // Other Libs
    implementation 'com.airbnb.android:lottie:6.0.0'
    implementation 'com.google.android.material:material:1.9.0'
    implementation 'com.google.android.gms:play-services-basement:18.2.0'
    implementation 'org.jetbrains.kotlinx:kotlinx-serialization-json:1.5.1'
    implementation 'com.squareup.okhttp3:logging-interceptor:5.0.0-alpha.11'

    detektPlugins "io.gitlab.arturbosch.detekt:detekt-formatting:$detekt_version"

    // Tests
    testImplementation project(":test")
    testImplementation 'androidx.arch.core:core-testing:2.2.0'

    androidTestImplementation 'androidx.test.ext:junit:1.1.5'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.1'
    androidTestImplementation "org.jetbrains.kotlinx:kotlinx-coroutines-test:$coroutines_version"

    // Debug
    debugImplementation 'com.squareup.leakcanary:leakcanary-android:2.11'
}
